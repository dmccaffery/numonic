#! /usr/bin/env zsh

__numonic_nvm_use() {
	# determine if nvm find nvmrc is available
	if ! command -v nvm_find_nvmrc 1>/dev/null 2>&1; then
		# move on immediately
		return 0
	fi

	nvmrc_path=$(nvm_find_nvmrc)
	current_node_version=$(nvm version)

	# determine if a path was found
	if [ -z "${nvmrc_path}" ]; then

		# unset the warning
		unset AUTOSWITCH_NODE_SUPPRESS_WARNING

		# determine if the current version is not the default
		if [ "${current_node_version}" != $(nvm version default) ]; then

			# use default version or lts version or just ignore
			nvm use default 2>/dev/null || nvm use --lts 2>/dev/null || \
				print-warn 'You should set/install a default version of node using: `nvm install default --lts` or similar.'
		fi

		# move on immediately
		return 0
	fi

	# read the nvmrc file to get the version
	requested_node_version=$(cat "${nvmrc_path}")

	# match the version
	matched_node_version=$(nvm_match_version ${requested_node_version})

	# determine if a version could be matched
	if [[ -n "${matched_node_version}" && "${matched_node_version}" != "N/A" ]]; then

		# unset the warning
		unset AUTOSWITCH_NODE_SUPPRESS_WARNING

		# use the matched version unless its already the current version
		if [ "${current_node_version}" != "${matched_node_version}" ]; then

			# use the specified version
			nvm use "${requested_node_version}"
		fi

		return 0
	fi

	# determine if we already warned about this path
	if [ "${AUTOSWITCH_NODE_SUPPRESS_WARNING}" = "${nvmrc_path}" ]; then

		# move on immediately
		return 0
	fi

	# get the relative path
	relative_nvm_path=${nvmrc_path##*\/}

	print-warn '' \
		'WARNING: Node.JS version requested is not installed.' \
		"	nvmrc path        : ${relative_nvm_path}" \
		"	requested version : ${requested_node_version}" \
		"	current version   : ${current_node_version}" \
	''

	# prompt the user to ask if they want to install now
	if read -q "RES?${CLR_WARN}Would you like to install ${requested_node_version} [y/N]: ${FORMAT_CLEAR}"; then

		# add some whitespace
		printf "\n\n"

		# install
		nvm install

		# move on immediately
		return 0
	fi

	# track the suppression warning
	export AUTOSWITCH_NODE_SUPPRESS_WARNING=${nvmrc_path}
}

# add a hook to switch nvm version when changing directories
autoload -U add-zsh-hook
add-zsh-hook chpwd __numonic_nvm_use

# invoke the initial check
__numonic_nvm_use
